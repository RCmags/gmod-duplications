@name Tank steering control
@inputs E:entity W S A D R Space
@outputs Active Throttle Gear LeftClutch RightClutch LeftBrake RightBrake 

interval(250)

if( ~R & R ) { Active = !Active } 

Throttle = Active*100

V = E:velL():x()

if( V > 100 ) {
    
    Clutch = W*( 1 - Space )
    
    Gear = round( 5 + V/550 )
    
    Brake = Space + S
    
}

if( abs( V ) <= 100 ) {
    
    Clutch = sign( W + S )*( 1 - Space )
    
    Gear = 5 - S
    
    Brake = Space
    
} 

if( V < -100 ) {
    
    Clutch = S*( 1 - Space )
    
    Gear = round( 4 + V/550 )
    
    Brake = Space + W 
    
}

K = 1/(1 + (V/600)^2 )

SteerLeft = K*D
SteerRight = K*A

LeftClutch = 1 - Clutch*( 1 - SteerLeft )

RightClutch = 1 - Clutch*( 1 - SteerRight )

LeftBrake = 20*clamp( Brake + SteerLeft , 0 , 1 ) 

RightBrake = 20*clamp( Brake + SteerRight , 0 , 1 )  


