@name Lift and drag 
@inputs P:wirelink

interval(10)

E = P:entity()

#Local velocites:

V = E:velL()/3997.4

Vx = V:x()

Vy = V:y()

Vz = V:z()

#Squared velocity magnitude

Vm = V:length2()

#Individual angles per plane:

# X

A = atan(-Vy/sqrt(Vx^2 + Vz^2))

X = atan(Vz/abs(Vx))

# Y

B = atan(-Vx/sqrt(Vy^2 + Vz^2))

Y = atan(Vz/abs(Vy))

# Z

C = atan(-Vz/sqrt(Vx^2 + Vy^2))

Z = atan(Vy/abs(Vx))

#Bounding box dimensions
D = E:boxSize()

Bx = D:x()

By = D:y()

Bz = D:z()

#Net area
An = Bz*(Bx + By) + Bx*By

#Lift coefficients per plane:

Lx = sin(A*2)

Ly = sin(B*2)

Lz = sin(C*2)

#Drag coefficients per plane:

Dx = 2*sin(A)^2

Dy = 2*sin(B)^2

Dz = 2*sin(C)^2

#Thickness ratio is the aspect ratio at 90 degrees from its corresponding plane

Ax = abs(cos(X)*Bz/Bx) + abs(sin(X)*Bx/Bz)
Ay = abs(cos(Y)*Bz/By) + abs(sin(Y)*By/Bz)
Az = abs(cos(Z)*By/Bx) + abs(sin(Z)*Bx/By)

#Aerodynamic force coefficient per axis:

Fx = Lx*sin(A)*cos(X) + Ly*cos(B) + Lz*sin(C)*cos(Z) - cos(A)*Dx*cos(X) + sin(B)*Dy - cos(C)*Dz*cos(Z)

Fy = Lx*cos(A) + Ly*sin(B)*cos(Y) + Lz*sin(C)*sin(Z) + sin(A)*Dx - cos(B)*Dy*cos(Y) - cos(C)*Dz*sin(Z)

Fz = Lx*sin(A)*sin(X) + Ly*sin(B)*sin(Y) + Lz*cos(C) - cos(A)*Dx*sin(X) - cos(B)*Dy*sin(Y) + sin(C)*Dz

#                   X                Y            Z 
E:applyForce((E:forward()*Fx - E:right()*Fy + E:up()*Fz)*Vm*airDensity()*An)


