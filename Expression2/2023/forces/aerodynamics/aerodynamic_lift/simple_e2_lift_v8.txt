@name Simple E2 Lift - v8 - aspect ratio
@inputs E:entity
@outputs [Xms Xn]:vector 

@persist Gain:vector 
@persist DT_MS DT 
@persist LMAX 
@persist CONST

@persist Lx Ly Lmax Area

# First call
if( first() ) {
    #--- Constants ---
    DT = tickInterval()
        
    # Decay ratio 
    Gain = vec(0,0,0)
    LMAX = 1.0              # Oscillates for L > 1
        
    #--- Functions ----
    
    # Set Prop velocity
    function void setVelocity( Vel:vector ) {
        Impulse = E:mass() * ( Vel - E:vel() )
        E:applyForce( Impulse )
    }
    
    # Step Restriction    
    function vector stepLimit( Dv:vector, Vel:vector, I ) {
        Vdot = Vel:dot(Dv)
        if( Vdot != 0 ) {
            # Measure decay ratio
            Lmeas = -Dv:length2() / Vdot
            # Set gain
            if( Lmeas > LMAX ) {
                Gain[I] = LMAX / Lmeas
            } else {
                Gain[I] = 1
            }
        }
        # Modify step 
        Dv = Dv * Gain[I]
        return Dv       
    }
    
    #--------

    #- Force functions: 
    Scale = 4e-6     # Scaling factor to match default drag: 4e-6
    
    # Surface area
    Size = E:boxSize()  
    Lx = Size:x() 
    Ly = Size:y() 
    Lmax = max(Lx,Ly)
        # Surface area 
    Area = Lx * Ly * pi() / 4

    # Multiplier
    CONST = 0.5 * airDensity() * Area * Scale
        
    # Aerodynamic ratios 
    function table aspectRatio(V:vector) {
        # unit vectors
        Ux = E:forward()
        Uy = E:right()
        Uz = E:up()
            
        # projected velocity
        Vxy = V - Uz:dot(V) * Uz
        Vmag = Vxy:length()
        
        # prevent zero division
        if( Vmag > 0 ) {
            Vnorm = Vxy/Vmag
            
            # 1. tangent slope
            P1 = abs( Ux:dot(Vnorm) )
            P2 = abs( Uy:dot(Vnorm) )
            
            if( P1 > P2 ) {
                M = P2/P1
                A = Ly
                B = Lx
            } else {
                M = P1/P2
                A = Lx
                B = Ly
            }
            
            # 2. intercept values
            M2 = M^2
            Y = M2/(M2 + (A/B)^2)
            
            Xo = B*sqrt(Y)
            Yo = A*sqrt(1 - Y)
            
            # 3. projected span
            Span = (Yo + M*Xo)/sqrt(1 + M2)  
                # Aspect Ratio
            AR = Span^2 / Area
                # Effective Chord
            Chord = Area/Span
        
        # Default case
        } else {
            Span = Lmax
            Chord = Area/Span
            AR = Span/Chord
        }
        return table(AR, Chord, Span)
    }
    
    function vector aeroCoeffs(V:vector, Alpha) {
        # geometry
        Out = aspectRatio(V)
        AR = Out[1, number]
        #print(AR)
        
        # Lift
        Slope = AR/(2 + AR)        
        Slope *= pi()
        Cl = Slope * sinr(2*Alpha)
        
        # Drag
            # induced
        Cd_max = 1 - AR/(AR^2 + 1)
        Cd_max *= pi()
        Cdi = Cd_max * sinr(Alpha)^2
            # no lift
        Cdo = 0.005
        Cd = Cdo + Cdi
        
        # Moment
        Cm = 0
        return vec(Cl, Cd, Cm)
    }
    
    function table aeroForces(V:vector) {    
        # Force normal vectors
        Wnorm = E:up()
        Lnorm = ( Wnorm:cross(V):cross(V) ):normalized()
        Dnorm = -V:normalized()
        
        # Angle of attack
        Vz = Wnorm:dot(V)     # velocity component about z-axis
        Vxy = V - Vz*Wnorm    # velocity about xy plane
        Alpha = atanr( Vz, Vxy:length() )
        
        # Lift and drag coefficients
        Coeffs = aeroCoeffs(V, Alpha)
        Cl = Coeffs[1]
        Cd = Coeffs[2]
        
        # Dynamic pressure
        Rho = CONST * V:length2() 
        Rho_xy = CONST * Vxy:length2()
    
        # Aerodynamic forces
        Lift = Rho * Cl * Lnorm
        Drag = Rho * Cd * Dnorm
        Drag_xy = Rho_xy * Cdo * Dnorm
        
        # Forces  
        return table(Lift, Drag, Drag_xy)
    }    
} # end, First call

# Iterate
runOnTick(1)

# 1. Measure velocity
Xms = E:vel()

# 2a. Estimate acceleration
    # Aerodynamic forces
Out = aeroForces(Xms)    
F1 = Out[1,vector]
F2 = Out[2,vector]
F3 = Out[3,vector]

    # accelerations
Mass = E:mass()  
A1 = (F1 + F2) / Mass
A2 = F3 / Mass

# 2b. Estimate velocity changes: Explicit euler
Dx1 = DT*A1          
Dx2 = DT*A2

# 3. Clamp velocity changes
    # velocity components
Uz = E:up()
Xz = Uz * Xms:dot(Uz)
Xxy = Xms - Xz
    # Local projections preserve lift/drag ratio
Dx1 = stepLimit(Dx1, Xz, 1)
Dx2 = stepLimit(Dx2, Xxy, 2)

# 4. Update state
Dx = Dx1 + Dx2
Xn = Xms + Dx

# 5. Set Prop state   [Must come after velocity measurement, or blocks measurement]
setVelocity( Xn )

# Note: Improve trajectory with additional iteration? Trapezoidal method?
