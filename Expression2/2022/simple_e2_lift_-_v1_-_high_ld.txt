@name Simple E2 Lift - v1 - highld
@inputs E:entity
@outputs Lnorm:vector Dnorm:vector
@outputs Alpha Cl Cd
@outputs Lift:vector Drag:vector
@persist 
@trigger 

runOnTick(1)

Vel = E:vel()

Vnorm = Vel:normalized()
Wnorm = E:up()

Lnorm = -(Vel:cross(Wnorm):cross(Vel)):normalized()
Dnorm = -Vnorm

Size = E:boxSize()
Area = Size:x()*Size:y()

Scale = 1e-7

Dyn_press = 0.5 * airDensity() * Vel:length2() * Area * Scale

Vl = E:velL()
Alpha = atan( Vl:z(), sqrt(Vl:x()^2 + Vl:y()^2) )

Cl = pi() * sin(2*Alpha)  

CDI = 8e-2
if( abs(Alpha) < 15 ) {
    Cd = CDI*(Alpha/15)^2
} else {
    Cd = CDI + (pi() - CDI)*sin(Alpha)^2  
}

Lift = Dyn_press * Cl * Lnorm
Drag = Dyn_press * Cd * Dnorm

E:applyForce( Lift + Drag )


